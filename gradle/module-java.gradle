apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply from: rootProject.file("./gradle/dependencies-kotlin.gradle")

sourceCompatibility = "8"
targetCompatibility = "8"

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion = versions.jacoco
}

build.finalizedBy jacocoTestReport

// Allows to set and use jacocoExcludes via extension
project.afterEvaluate {
    jacocoTestCoverageVerification {
        violationRules {
            rule {
                element = 'CLASS'
                limit {
                    counter = 'INSTRUCTION'
                    minimum = 0.7
                }

                if (project.hasProperty("jacocoExcludes")) {
                    excludes = project.property("jacocoExcludes")
                }
            }
        }
    }
}

jacocoTestReport.finalizedBy jacocoTestCoverageVerification

dependencies {

    implementation "org.threeten:threetenbp:${versions.threeten.java}:no-tzdb"

    testImplementation "org.threeten:threetenbp:${versions.threeten.java}"

    testImplementation "com.google.truth:truth:${versions.truth}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${versions.junit.jupiter}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${versions.junit.jupiter}"
}
